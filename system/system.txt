Bool:
  bool a:True = Bool a
  bool a:False = Bool a

True:
  true = True

False:
  false = False

id a = a
dot a b f = a (b f)

not True = false
not False = true

bnot (Bool a) = bool (not a)

or True * = true
or False a = a

and False * = false
and True a = a

eq a a = true
eq * * = false

ite True a b = a
ite False a b = b

----------------------------------------------------------------------------------------------------

Nat:
  nat a:Zero = Nat a
  nat a:(Succ *) = Nat a

Zero:
  zero = Zero

Succ:
  succ_ (Nat a) = Succ a

succ a = succ_ (nat a)

----------------------------------------------------------------------------------------------------

List:
  list a:(Nil ~t) = List ~t a
  list a:(Cons ~t * *) = List ~t a

Nil:
  nil ~t = Nil ~t

Cons:
  cons_ a:(t *) (List ~t b) = Cons ~t a b

cons a b = cons_ a (list b)

map ~t f (Nil *) = nil ~t
map ~t f (Cons * x xs) = cons (f x) (map ~t f xs)

foldl f z (Nil *) = z
foldl f z (Cons * x xs) = foldl f (f z x) xs

all f list = foldl (all_1 f) true list
all_1 f a b = and a (f b)

any f list = foldl (any_1 f) false list
any_1 f a b = or a (f b)

----------------------------------------------------------------------------------------------------

Pred:
  pred a:(Pident *) = Pred a
  pred a:(Impl * *) = Pred a
  pred a:(Pnot *) = Pred a
  pred a:(Forall * *) = Pred a
  pred a:(Pelem * *) = Pred a

Pident:
  pident_ (Nat a) = Pident a

Impl:
  impl_ (Pred a) (Pred b) = Impl a b

Pnot:
  pnot_ (Pred a) = Pnot a

Forall:
  forall_ (Nat a) (Pred b) = Forall a b

Pelem:
  pelem_ (Nat a) (Nat b) = Pelem a b

pident a = pident_ (nat a)
impl a b = impl_ (pred a) (pred b)
pnot a = pnot_ (pred a)
forall a b = forall_ (nat a) (pred b)
pelem a b = pelem_ (nat a) (nat b)

a. = pident zero
b. = nextPident a.
c. = nextPident b.
d. = nextPident c.

nextPident (Pident a) = pident (succ a)

substPident pident:(Pident *) p pident = p
substPident pident:(Pident *) p a:(Pident *) = a
substPident pident:(Pident *) p (Impl a b) = impl (substPident pident p a) (substPident pident p b)
substPident pident:(Pident *) p (Pnot a) = pnot (substPident pident p a)
substPident pident:(Pident *) p (Forall a b) = forall a (substPident pident p b)
substPident pident:(Pident *) p a:(Pelem * *) = a

substIdent ident1 ident2 (Impl a b) = impl (substIdent ident1 ident2 a) (substIdent ident1 ident2 b)
substIdent ident1 ident2 (Pnot a) = pnot (substIdent ident1 ident2 a)
substIdent ident1 ident2 x:(Forall ident1 b) = x
substIdent ident1 ident2 (Forall a b) = forall a (substIdent ident1 ident2 b)
substIdent ident1 ident2 (Pelem a b) = pelem (replaceIdent ident1 ident2 a) (replaceIdent ident1 ident2 b)

replaceIdent ident1 ident2 ident1 = ident2
replaceIdent ident1 ident2 * = ident1

hasPident pident:(Pident *) pident = true
hasPident pident:(Pident *) (Pident *) = false
hasPident pident:(Pident *) (Impl a b) = or (hasPident pident a) (hasPident pident b)
hasPident pident:(Pident *) (Pnot a) = hasPident pident a
hasPident pident:(Pident *) (Forall * a) = hasPident pident a
hasPident pident:(Pident *) (Pelem a b) = false

hasFreeIdent ident (Impl a b) = or (hasFreeIdent ident a) (hasFreeIdent ident b)
hasFreeIdent ident (Pnot a) = hasFreeIdent ident a
hasFreeIdent ident (Forall ident *) = false
hasFreeIdent ident (Forall * a) = hasFreeIdent ident a
hasFreeIdent ident (Pelem a b) = or (eq a ident) (eq b ident)

nextAvailPident p pident = ite (hasPident pident p)
  nextAvailPident_1 nextAvailPident_2 p pident
nextAvailPident_1 p pident = nextAvailPident p (nextPident pident)
nextAvailPident_2 p pident = pident

----------------------------------------------------------------------------------------------------

Proof:
  rule.inst_ ident:(Pident *) (Pred p) (Proof phi) = Proof (substPident ident p phi)
  rule.mp (Proof (Impl a b)) (Proof a) = Proof b

  ax.prop.1 = Proof (impl a. (impl b. a.))
  ax.prop.2 = Proof (impl (impl a. (impl b. c.)) (impl (impl a. b.) (impl a. c.)))
  ax.prop.3 = Proof (impl (impl (pnot a.) (pnot b.)) (impl b. a.))

rule.inst a b c = rule.inst_ a (pred b) c

----------------------------------------------------------------------------------------------------

proof2pred (Proof a) = a

----------------------------------------------------------------------------------------------------

-- util.prop.sup.add sup proof = rule.mp
--   (rule.inst th.prop.ax1.a.b)
--   proof

----------------------------------------------------------------------------------------------------

-- a -> b -> a
th.prop.ax1.a.b = ax.prop.1

-- a -> (b -> a) -> a
th.prop.ax1.a.ba = rule.inst b. (impl b. a.) th.prop.ax1.a.b

-- (a -> b -> c) -> (a -> b) -> a -> c
th.prop.ax2.a.b.c = ax.prop.2

-- (a -> b -> a) -> (a -> b) -> a -> a
th.prop.ax2.a.b.a = rule.inst c. a. th.prop.ax2.a.b.c

-- (a -> (b -> a) -> a) -> (a -> b -> a) -> a -> a
th.prop.ax2.a.ba.a = rule.inst b. (impl b. a.) th.prop.ax2.a.b.a

-- (a -> b -> a) -> a -> a
th.prop.impl.ax1ab.refl = rule.mp th.prop.ax2.a.ba.a th.prop.ax1.a.ba

-- a -> a
th.prop.impl.refl = rule.mp th.prop.impl.ax1ab.refl th.prop.ax1.a.b

----------------------------------------------------------------------------------------------------

main = func ~func
func (a *) = ~a